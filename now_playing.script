#!/usr/bin/env osascript
# Returns the current playing song in Spotify for OSX

set iTunes_up to false
set iTunes_playing to false
set iTunes_track_info to ""

set spotify_up to false
set spotify_playing to false
set spotify_track_info to ""

tell application "System Events"
	set process_list to (name of every process)
end tell

if process_list contains "iTunes" then
	set iTunes_up to true

	tell application "iTunes"
		set iTunes_playing to (player state is playing)

		try
			set track_name to name of current track
			set artist_name to artist of current track

			set iTunes_track_info to artist_name & " - " & track_name
		end try
	end tell
end if

if process_list contains "Spotify" then
	set spotify_up to true

	tell application "Spotify"
		set spotify_playing to (player state is playing)
		set track_name to name of current track
		set artist_name to artist of current track

		set spotify_track_info to artist_name & " - " & track_name
	end tell
end if

set paused_pref to "[P] "
set playing_pref to ""

# playing
if spotify_playing then
	set pref to playing_pref
	set track_info to spotify_track_info
else if iTunes_playing then
	set pref to playing_pref
	set track_info to iTunes_track_info
else if iTunes_up and (iTunes_track_info isn't "") then # paused
	set pref to paused_pref
	set track_info to iTunes_track_info
else if spotify_up then
	set pref to paused_pref
	set track_info to spotify_track_info
end if

set now_playing to pref & track_info
